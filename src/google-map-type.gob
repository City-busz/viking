%headertop{
#ifdef HAVE_CONFIG_H
#include "config.h"
#endif

#include "vikcoord.h"
#include "mapcoord.h"
#include "slippy-map-type-abstract.h"
%}

%{

#include <stdio.h>
#include <stdlib.h>
#include <string.h>
#include <glib/gi18n.h>
#include <gtk/gtk.h>
#include <math.h>
#include <string.h>
#include "coords.h"
#include "vikcoord.h"
#include "mapcoord.h"
#include "download.h"
#include "curl_download.h"
#include "globals.h"
#include "google.h"
#include "vikmapslayer.h"
%}

%h{

typedef enum {
	TYPE_GOOGLE_MAPS = 0,
	TYPE_GOOGLE_TRANS,
	TYPE_GOOGLE_SAT,
	TYPE_GOOGLE_TERRAIN,

	TYPE_GOOGLE_NUM
} GoogleType;

%}

%{

static DownloadOptions google_options = { "http://maps.google.com/", 0, a_check_map_file };

static gchar *parse_version_number(gchar *text)
{
  int i;
  gchar *vers;
  gchar *s = text;

  for (i = 0; (s[i] != '\\') && (i < 8); i++)
    ;
  if (s[i] != '\\') {
    return NULL;
  }

  return vers = g_strndup(s, i);
}

const gchar *google_version_number(GoogleMapType *self, MapCoord *mapcoord, GoogleType google_type)
{
  static gboolean first = TRUE;
  static char *vers[] = { "w2.80", "w2t.80", "30", "w2p.81"};
  FILE *tmp_file;
  int tmp_fd;
  gchar *tmpname;
  gchar *uri;
  VikCoord coord;
  gchar coord_north_south[G_ASCII_DTOSTR_BUF_SIZE], coord_east_west[G_ASCII_DTOSTR_BUF_SIZE];
  gchar *text, *pat, *beg;
  GMappedFile *mf;
  gsize len;
  gchar *gvers, *tvers, *kvers, *terrvers, *tmpvers;
  static DownloadOptions dl_options = { "http://maps.google.com/", 0, a_check_map_file };
  /* static const char *gvers_pat = "http://mt0.google.com/mt?v\\x3d"; */
  static const char *gvers_pat = "http://mt0.google.com/mt";
  static const char *kvers_pat = "http://khm0.google.com/kh?v\\x3d";

  g_assert(google_type < TYPE_GOOGLE_NUM);

  if (!first)
    return (vers[google_type]);


  first = FALSE;
  gvers = tvers = kvers = terrvers = NULL;
  if ((tmp_fd = g_file_open_tmp ("vikgvers.XXXXXX", &tmpname, NULL)) == -1) {
    g_critical(_("couldn't open temp file %s"), tmpname);
    exit(1);
  } 

  vik_map_type_mapcoord_to_center_coord(VIK_MAP_TYPE(self), mapcoord, &coord);
  uri = g_strdup_printf("http://maps.google.com/maps?f=q&hl=en&q=%s,%s",
                        g_ascii_dtostr (coord_north_south, G_ASCII_DTOSTR_BUF_SIZE, (gdouble) coord.north_south),
                        g_ascii_dtostr (coord_east_west, G_ASCII_DTOSTR_BUF_SIZE, (gdouble) coord.east_west));
  tmp_file = fdopen(tmp_fd, "r+");

  if (curl_download_uri(uri, tmp_file, &dl_options)) {  /* error */
    g_warning(_("Failed downloading %s"), tmpname);
  } else {
    if ((mf = g_mapped_file_new(tmpname, FALSE, NULL)) == NULL) {
      g_critical(_("couldn't map temp file"));
      exit(1);
    }
    len = g_mapped_file_get_length(mf);
    text = g_mapped_file_get_contents(mf);

    if ((beg = g_strstr_len(text, len, "GLoadApi")) == NULL) {
      g_warning(_("Failed fetching Google numbers (\"GLoadApi\" not found)"));
      goto failed;
    }

    pat = beg;
    while (!gvers || !tvers ||!terrvers) {
      if ((pat = g_strstr_len(pat, &text[len] - pat, gvers_pat)) != NULL) {
        pat += strlen(gvers_pat);
	if ((pat[0] != '/' && pat[0] != '?') ||
	     pat[1] != 'v' || pat[2] != '\\' ||
	     pat[3] != 'x' || pat[4] != '3' || pat[5] != 'd')
          continue;
        pat += 6;
        if ((tmpvers = parse_version_number(pat)) != NULL) {
          if (strstr(tmpvers, "t."))
            tvers = tmpvers;
          else if (strstr(tmpvers, "p."))
            terrvers = tmpvers;
          else
            gvers = tmpvers;
        }
      }
      else
        break;
    }

    if ((pat = g_strstr_len(beg, &text[len] - beg, kvers_pat)) != NULL)
        kvers = parse_version_number(pat + strlen(kvers_pat));

    if (gvers && tvers && kvers) {
      vers[TYPE_GOOGLE_MAPS] = gvers;
      vers[TYPE_GOOGLE_TRANS] = tvers;
      vers[TYPE_GOOGLE_SAT] = kvers;
      vers[TYPE_GOOGLE_TERRAIN] = terrvers;
    }
    else
      g_warning(_("Failed getting google version numbers"));

    if (gvers)
      fprintf(stderr, "DEBUG gvers=%s\n", gvers);
    if (tvers)
      fprintf(stderr, "DEBUG tvers=%s\n", tvers);
    if (terrvers)
      fprintf(stderr, "DEBUG terrvers=%s\n", terrvers);
    if (kvers)
      fprintf(stderr, "DEBUG kvers=%s\n", kvers);

failed:
    g_mapped_file_free(mf);
  }

  fclose(tmp_file);
  tmp_file = NULL;
  g_free(tmpname);
  g_free (uri);
  return (vers[google_type]);
}

%}

class Google:Map:Type from Slippy:Map:Type:Abstract {
  private GoogleType type;

  init (self) {
          /* initialize the object here */
          VIK_MAP_TYPE(self)->tilesize_x = 256;
          VIK_MAP_TYPE(self)->tilesize_y = 256;
          VIK_MAP_TYPE(self)->drawmode = VIK_VIEWPORT_DRAWMODE_MERCATOR;
  }

  public GObject *
  new_with_id (guint8 id, GoogleType type) {
	  GoogleMapType *ret = GET_NEW;
	  VIK_MAP_TYPE(ret)->uniq_id = id;
	  ret->_priv->type = type;
	  return G_OBJECT (ret);
  }

  override (Slippy:Map:Type:Abstract) gchar *
  get_uri( Slippy:Map:Type:Abstract *self, MapCoord *src )
  {
     const gchar *verstr = google_version_number(GOOGLE_MAP_TYPE(self), src, GOOGLE_MAP_TYPE(self)->_priv->type);
     gchar *uri = g_strdup_printf ( "/mt?n=404&v=%s&x=%d&y=%d&zoom=%d", verstr, src->x, src->y, src->scale );
     return uri;
  }

  override (Slippy:Map:Type:Abstract) gchar *
  get_hostname( Slippy:Map:Type:Abstract *self )
  {
    return g_strdup("mt.google.com");
  }

  override (Slippy:Map:Type:Abstract) DownloadOptions *
  get_download_options( Slippy:Map:Type:Abstract *self )
  {
    return &google_options;
  }
}

%h{
const gchar *google_version_number(GoogleMapType *self, MapCoord *mapcoord, GoogleType google_type);
%}